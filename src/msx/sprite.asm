; ====================================================================================================
;
; sprite.asm
;
; included from main.asm
;
; ====================================================================================================
SECTION code_user

INCLUDE "player.asm"
INCLUDE "enemy1.asm"
INCLUDE "enemy2.asm"

; ====================================================================================================
; スプライトキャラクターワークテーブルのアドレス値を求める
; SPR_CHR_WK_TBL+(キャラクターNo*16)のアドレスを求めてIXレジスタに設定します。
; DE,HLレジスタの値を破壊します。
; IN  : A = キャラクターNo(1〜)
; OUT : IX = 求めたスプライトキャラクターワークテーブルのアドレス
; ====================================================================================================
GET_SPR_WK_ADDR:
    ; ■スプライトキャラクターワークテーブルの先頭アドレス
    LD IX,SPR_CHR_WK_TBL            ; IXに設定

    ; ■算出対象かチェック
    OR A
    JR Z,GET_SPR_WK_ADDR_EXIT       ; A=0ならそのまま終了する

    ; ■オフセット値算出
    ; DEレジスタにA*16を求める
    DEC A
    LD H,0                          ; HL=A
    LD L,A

    ADD HL,HL                       ; HL=HL*16
    ADD HL,HL
    ADD HL,HL
    ADD HL,HL

    LD D,H                          ; HL -> DE
    LD E,L

    ; ■スプライトキャラクターワークテーブルのアドレス算出
    LD HL,SPR_CHR_WK_TBL            ; HL=スプライトキャラクターワークテーブルの先頭アドレス
    ADD HL,DE                       ; HL=HL+DE

    PUSH HL                         ; HL -> IX
    POP IX

GET_SPR_WK_ADDR_EXIT:
    RET 


; ====================================================================================================
; スプライトキャラクター移動処理
; 事前にIXにスプライトキャラクターワークテーブルの先頭アドレスを設定済であること
; @ToDo:IXレジスタの使用を最小限に抑えたい
; ====================================================================================================
SPRITE_MOVE:
    ; ■対象スプライトキャラクターの移動方向から移動量データのアドレス算出
    LD A,(IX+7)                     ; A=移動方向
    OR A                            ; 移動方向が0(=移動しない)場合は終了
    JR Z,SPRITE_MOVE_EXIT

    ; ■移動量データの取得
    RLCA                            ; A=A*2
    RLCA                            ; A=A*2、ここで移動量データのオフセットがAに設定される

    LD HL,MOVE_DATA                 ; HLレジスタに移動量データのアドレスを設定
    ADD HL,A

    PUSH HL                         ; HL -> IY
    POP IY

    ; ■Y座標計算
    LD H,(IY+1)                     ; BC <- 移動量データ(Y方向)
    LD L,(IY)
    LD A,(IX+8)                     ; A <- 移動量データ
    CALL CALCULATE_MOVE_VALUE       ; 移動量計算
    LD B,H
    LD C,L

    LD H,(IX+2)                     ; HL <- Y座標
    LD L,(IX+1)
    ADC HL,BC                       ; HL=HL+BC

    ; ■Y座標画面下限チェック
    LD A,H                          ; A <- Y座標(整数部)
    CP 191-24                       ; 画面下限を超えたか
    JR C,SPRITE_MOVE_L0             ; キャリーフラグがONの場合は画面内なのでSPRITE_MOVE_L0へ
 
    LD H,191-24                     ; Y座標(整数部)=(191-16)
    LD L,0                          ; Y座標(小数部)=0
    JR C,SPRITE_MOVE_L1             ; 上限の判定は不要なので次のチェックへ

SPRITE_MOVE_L0:
    ; ■Y座標画面上限チェック
    CP 8                            ; 画面上限を超えたか
    JR NC,SPRITE_MOVE_L1            ; キャリーフラグがOFFの場合は画面内なのでSPRITE_MOVE_L1へ
    LD H,8                          ; Y座標(整数部)=8
    LD L,0                          ; Y座標(小数部)=0

SPRITE_MOVE_L1:
    ; ■Y座標を保存
    LD (IX+2),H                     ; H -> Y座標(整数部)
    LD (IX+1),L                     ; L -> Y座標(小数部)

    ; ■X座標計算
    LD H,(IY+3)                     ; BC <- 移動量データ(X方向)
    LD L,(IY+2)
    LD A,(IX+8)                     ; A <- 移動量データ
    CALL CALCULATE_MOVE_VALUE       ; 移動量計算
    LD B,H
    LD C,L

    LD H,(IX+4)                     ; HL <- X座標
    LD L,(IX+3)
    ADC HL,BC                       ; HL=HL+BC

    ; ■X座標画面端チェック
    LD A,H                          ; 座標値チェック
    CP 255-16                       ; A=A-(255-16)
    JR C,SPRITE_MOVE_L2             ; キャリーフラグがONの場合は画面内なのでSPRITE_MOVE_L2へ

    ; ■一旦右端として座標値を設定する
    LD H,255-16                     ; X座標(整数部)=255-15
    LD L,0                          ; X座標(小数部)=0

    OR A
    CP 255-7                        ; 最大移動量を4と仮定した比較
    JR C,SPRITE_MOVE_L2             ; キャリーフラグがOFF(=画面左部にはみ出てた場合)はHはそのままで良いのでSPRITE_MOVE_L2へ

    LD H,0                          ; X座標(整数部)=0
    LD L,0                          ; X座標(小数部)=0

SPRITE_MOVE_L2:
    ; ■X座標を保存
    LD (IX+4),H
    LD (IX+3),L

SPRITE_MOVE_EXIT:
    RET


; ====================================================================================================
; 移動量計算サブルーチン
; IN  : HL = 移動量データ(上位＝小数部、下位＝整数部)
;       A = 移動量(ビット7＝0:左シフト、1:右シフト、ビット6〜0＝シフト量) 
; 破壊: BC
; ====================================================================================================
CALCULATE_MOVE_VALUE:
    ; ■移動量のビット7が立っていたら除算、以外は乗算
    PUSH AF
    AND %10000000
    JR NZ,CALCULATE_MOVE_VALUE_DIVIDE

 ALCULATE_MOVE_VALUE_MULTIPLE:
    ; ■Aレジスタの値をBレジスタに設定
    POP BC

CALCULATE_MOVE_VALUE_MULTIPLE_L1:
    ADD HL,HL

    DJNZ CALCULATE_MOVE_VALUE_MULTIPLE_L1

    RET

CALCULATE_MOVE_VALUE_DIVIDE:
    ; ■Aレジスタの0〜6ビット目だけ取得
    POP AF
    AND %01111111

    ; ■Aレジスタの値をBレジスタに設定
    LD B,A

CALCULATE_MOVE_VALUE_DIVIDE_L1:
    SRL H
    RR L

    DJNZ CALCULATE_MOVE_VALUE_DIVIDE_L1

    RET


; ====================================================================================================
; キャラクター衝突判定処理
; 2つのキャラクターの座標を比較して、衝突していた場合はキャリーフラグを立てて戻る
; DE,HLレジスタの値を破壊します
; IN  : A = 比較元のスプライトキャラクターワークテーブルのインデックス
;       C = 比較先のスプライトキャラクターワークテーブルのインデックス
; OUT : キャリーフラグ(ON=衝突している、OFF=衝突していない)
; ====================================================================================================
HIT_CHECK:
    ; ■比較先のキャラクター番号からスプライトキャラクターワークテーブルのアドレスを求める
    ; - IXレジスタは比較元の情報として残しておきたいので、一度スタックに退避し、
    ;   ここで取得したアドレスはHLレジスタに入れておく
    PUSH IX

    LD A,C
    CALL GET_SPR_WK_ADDR
    PUSH IX                         ; IX -> HL
    POP HL

    POP IX

    ; ■Y座標の比較
    OR A                            ; キャリーフラグリセット
    LD A,(IX+2)                     ; A <- 比較元のY座標
    INC HL                          ; B <- 比較先のY座標
    INC HL
    LD B,(HL)                   
    CALL ABS_SUB                    ; 差分を絶対値で取得
    CP 10                           ; A < 10 の場合はキャリーフラグが立つ
    JR NC,HIT_CHECK_EXIT            ; キャリーフラグが立っていない場合は終了

    ; ■X座標の比較
    LD A,(IX+4)                     ; A <- 比較元のX座標
    INC HL                          ; B <- 比較先のX座標
    INC HL
    LD B,(HL)
    CALL ABS_SUB                    ; 差分を絶対値で取得
    CP 10                           ; A < 10 の場合はキャリーフラグが立つ

HIT_CHECK_EXIT:
    RET


; ====================================================================================================
; スプライトパターン番号更新サブルーチン
; ====================================================================================================
SPRITE_ANIM:
    ; ■アニメーションパターンテーブルのアドレス取得
    LD HL,ANIM_PTN_TBL              ; HL <- アニメーションパターンアドレステーブル
    LD A,(IX+8)                     ; A <- アニメーションテーブル番号
    CALL GET_ADDR_TBL               ; アドレステーブルからデータ取得
    LD HL,DE                        ; HL <- DE
                                    ; - DEはパターンリセット用に保持しておくためにEXしない

    ; ■アニメーションカウンタからアニメーションパターン番号のアドレス算出
    LD A,(IX+9)                     ; A <- アニメーションカウンタ
    INC A                           ; アニメーションカウンタを１つ進める
    PUSH AF                         ; アニメーションカウンタは一旦スタックに保存

    LD B,0                          ; BC <- アニメーションカウンタ
    LD C,A
    ADD HL,BC                       ; HL=HL+BC

	; ■アニメーションパターン番号を取得する
	LD A,(HL)					    ; A <- (HL) (=アニメーションパターン番号)
    OR 0						    ; 0(=アニメーションパターンの終端)かどうか 
    JR Z,SPRITE_ANIM_L2             ; 終端の場合はSPRITE_ANIM_L3にジャンプ

	; ■アニメーションパターンテーブルのアニメーションパターン番号を設定する
	SUB 1						    ; A=A-1（パターンテーブルの値はスプライトパターン番号+1が設定されているため）
	LD (IX+5),A		                ; A -> スプライトパターン番号

    POP AF                          ; アニメーションカウンタをスタックから取得
    LD (IX+9),A			            ; アニメーションカウンタ保存
    JR SPRITE_ANIM_EXIT

SPRITE_ANIM_L2:
	; ■アニメーションパターンテーブルの先頭のアニメーションパターン番号を設定する
	LD A,(DE)					    ; A <- (DE) (=アニメーションパターン番号の戦闘アドレス)
	SUB 1						    ; A=A-1（パターンテーブルの値はスプライトパターン番号+1が設定されているため）
	LD (IX+5),A		                ; A -> スプライトパターン番号

    POP AF                          ; アニメーションカウンタをスタックから取得
    XOR A                           ; A=0
    LD (IX+9),A			            ; アニメーションカウンタ保存

SPRITE_ANIM_EXIT:
    RET


; ====================================================================================================
; キャラクター登録サブルーチン
; １．スプライトキャラクターワークテーブルの空きを調べる
; ２．空いてなかったら終了する
; ３．空いていたらその要素に対して指定されたキャラクター番号の初期化処理を実行する
;
; 最大キャラクター数に達している場合は何もせずに終了する
; キャラクター登録可能な場合は、キャラクター登録数を増加させて対象キャラクター番号の
; データをスプライトキャラクターワークテーブルに登録する
; IN  : A = 対象のキャラクター番号(1〜)
; ====================================================================================================
ADD_CHARACTER:
    PUSH BC                         ; BCレジスタをスタックに退避
    PUSH AF                         ; AFレジスタ(=キャラクター番号)をスタックに退避

    LD B,MAX_CHR_CNT                ; B=最大キャラクター数
    LD HL,SPR_CHR_WK_TBL            ; HL <- スプライトキャラクターワークテーブル

ADD_CHARACTER_L1:
    LD A,(HL)                       ; A <- キャラクター番号
    OR A
    JR Z,ADD_CHARACTER_L2           ; キャラクター番号がゼロなら登録処理へ

    LD DE,16
    ADD HL,DE                       ; HL=HL+16
    DJNZ ADD_CHARACTER_L1

    POP AF
    POP BC
    RET

ADD_CHARACTER_L2:
    LD IX,HL                        ; IX <- HL

    POP AF                          ; AFレジスタ(=キャラクター番号)をスタックから復元
    DEC A
    LD HL,CHARACTER_INIT_TABLE      ; HL <- キャラクター初期化テーブルのアドレス
    CALL TBL_JP

ADD_CHARACTER_EXIT:
    POP BC
    RET


; ====================================================================================================
; キャラクター削除サブルーチン
; IN  : A = 対象のスプライトキャラクターワークテーブルのインデックス
; ====================================================================================================
DEL_CHARACTER:
    ; ■対象のキャラクター番号からスプライトキャラクターワークテーブルのアドレスを取得
    CALL GET_SPR_WK_ADDR

    ; ■スプライトキャラクターワークテーブルの属性をゼロにする
    LD (IX),0                       

DEL_CHARACTER_EXIT:
    RET


; ====================================================================================================
; スプライトキャラクターワークテーブルからスプライトアトリビュートワークテーブルを設定する
; ====================================================================================================
SET_SPR_ATR_WK:
    LD B,MAX_CHR_CNT                ; スプライトキャラクター分繰り返し
    LD HL,SPR_CHR_WK_TBL            ; スプライトキャラクターワークテーブルの先頭アドレス
    LD DE,SPR_ATR_WK_TBL            ; スプライトアトリビュートワークテーブルの先頭アドレス

SET_SPR_ATR_WK_L1:
    ; ■Y座標
    INC HL                          ; HL=HL+2 (Y座標の上位1バイトのアドレス)
    INC HL
    LD A,(HL)                       ; (HL)→A
    LD (DE),A                       ; A→(DE)

    ; ■X座標
    INC HL                          ; HL=HL+2 (X座標の上位1バイトのアドレス)
    INC HL
    LD A,(HL)                       ; (HL)→A
    INC DE                          ; DE=DE+1
    LD (DE),A                       ; A→(DE)

    ; ■スプライトパターンNo
    INC HL                          ; HL=HL+1
    LD A,(HL)                       ; (HL)→A
    ADD A,A                         ; A=A*4 (スプライトが16x16なので、パターンNoを4倍する)
    ADD A,A
    INC DE                          ; DE=DE+1
    LD (DE),A                       ; A→(DE)

    ; ■カラーコード
    INC HL                          ; HL=HL+1
    LD A,(HL)                       ; (HL)→A
    INC DE                          ; DE=DE+1
    LD (DE),A                       ; A→(DE)

    ; ■設定元のスプライトキャラクターワークテーブルのアドレスを次の先頭アドレスへ
    PUSH BC                         ; 3
    LD BC,10                        ; 3 HL=HL+10
    ADD HL,BC                       ; 3
    POP BC                          ; 3
    
    ; ■設定先のスプライトアトリビュートワークテーブルのアドレスを次の先頭アドレスへ
    INC DE                          ; DE=DE+1

    DJNZ SET_SPR_ATR_WK_L1

SET_SPR_ATR_WK_EXIT:
    RET


; ====================================================================================================
; スプライトアトリビュートエリア設定
; ====================================================================================================
SET_SPR_ATTR_AREA:
    LD HL,SPR_ATR_WK_TBL            ; スプライトアトリビュートワークテーブル
    LD DE,SPR_ATR_ADDR              ; スプライトアトリビュートエリア
    LD BC,4*MAX_CHR_CNT             ; 転送バイト数(4byte*キャラクター数)
    CALL LDIRVM                     ; BIOS VRAMブロック転送

SET_SPR_ATTR_AREA_EXIT:
    RET 


SECTION rodata_user
; ====================================================================================================
; 定数エリア
; romに格納される
; ====================================================================================================

MAX_CHR_CNT:            EQU 32      ; 最大キャラクター数
SPR_CHR_WK_SIZE:        EQU 16      ; スプライトキャラクターワークエリアのサイズ

; ■移動量データ
; Y座標、X座標の移動量をSTICKの値の順に定義
; 計算時の座標値は10倍とし、計算後の座標は1/10とする必要がある
; STICKの値は、キーボードとパッドの入力の OR を取る
; 9以降は両方同時に入力された時のためのダミーデータ(最大15となる)
MOVE_DATA:
    DW $0000,$0000                  ; STICK=0(未入力)
    DW $FF00,$0000                  ; STICK=1(上)
    DW $FF4F,$00B0                  ; STICK=2(右上)
    DW $0000,$0100                  ; STICK=3(右)
    DW $00B0,$00B0                  ; STICK=4(右下)
    DW $0100,$0000                  ; STICK=5(下)
    DW $00B0,$FF4F                  ; STICK=6(左下)
    DW $0000,$FF00                  ; STICK=7(左)
    DW $FF4F,$FF4F                  ; STICK=8(左上)
    DW $0000,$0000                  ; STICK=9
    DW $0000,$0000                  ; STICK=10
    DW $0000,$0000                  ; STICK=11
    DW $0000,$0000                  ; STICK=12
    DW $0000,$0000                  ; STICK=13
    DW $0000,$0000                  ; STICK=14
    DW $0000,$0000                  ; STICK=15

; ■キャラクター初期化テーブル
CHARACTER_INIT_TABLE:
    DW INIT_PLAYER                  ; PLAYER
    DW INIT_PLAYER2                 ; PLAYER2
    DW INIT_ENEMY1                  ; ENEMY1
    DW INIT_ENEMY2                  ; ENEMY2

; ■キャラクターロジックテーブル
CHARACTER_UPDATE_TABLE:
    DW UPDATE_PLAYER                ; PLAYER
    DW UPDATE_PLAYER2               ; PLAYER2
    DW UPDATE_ENEMY1                ; ENEMY1
    DW UPDATE_ENEMY2                ; ENEMY2

; ■アニメーションパターンアドレステーブル
ANIM_PTN_TBL:
    DW ANIM_PTN_ENEMY1_R
    DW ANIM_PTN_ENEMY1_L
    DW ANIM_PTN_ENEMY2


SECTION bss_user
; ====================================================================================================
; ワークエリア
; プログラム起動時にcrtでゼロでramに設定される 
; ====================================================================================================

; ■スプライトキャラクターワークテーブル(16Byte)
; +0:キャラクター番号
; +1:Y座標(小数部)
; +2:Y座標(整数部)
; +3:X座標(小数部)
; +4:X座標(整数部)
; +5:スプライトパターンNo(1～64)
; +6:カラーコード(0=非表示)
; +7:移動方向(STICKの値に対応)  
; +8:移動量(ビット7＝0:左シフト、1:右シフト、ビット6〜0＝シフト量)
; +9:アニメーションテーブル番号
; +10:アニメーションカウンタ
; +11:汎用
; +12:汎用
; +13:汎用
; +14:汎用
; +15:汎用
SPR_CHR_WK_TBL:
    DEFS SPR_CHR_WK_SIZE*MAX_CHR_CNT

; ■スプライトアトリビュートワークテーブル(4byte*n)
; +0:スプライトアトリビュート1バイト目(Y座標)
; +1:スプライトアトリビュート2バイト目(X座標)
; +2:スプライトアトリビュート3バイト目(スプライトパターンNo)
; +3:スプライトアトリビュート4バイト目(カラーコード)
SPR_ATR_WK_TBL:
	DEFS 4*MAX_CHR_CNT
